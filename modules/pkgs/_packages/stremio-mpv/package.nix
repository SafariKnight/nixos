{
  stdenvNoCC,
  fetchurl,
  patch,
  stremio,
  lib,
}: let
  patchedServer = stdenvNoCC.mkDerivation rec {
    pname = "stremio-server-patched";
    version = "4.20.8";

    src = fetchurl {
      url = "https://dl.strem.io/server/v${version}/desktop/server.js";
      hash = "sha256-cRMgD1d1yVj9FBvFAqgIqwDr+7U3maE8OrCsqExftHY=";
    };

    dontUnpack = true;

    installPhase = ''
      mkdir -p $out

      cp $src $out/server.js

      ${patch}/bin/patch -p1 -i ${./mpv.patch} $out/server.js
    '';

    meta.license = lib.licenses.unfree;
  };
in
  stremio.overrideAttrs (oldAttrs: {
    server = patchedServer;
    postInstall =
      oldAttrs.postInstall
      +
      # bash
      ''
        rm $out/opt/stremio/server.js
        ln -s ${patchedServer}/server.js $out/opt/stremio/server.js
      '';
  })
